plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.9.16'
}

ext.moduleName = 'org.consensusj.ledgerexport.tool'

application {
    mainClass = 'org.consensusj.ledgerexport.tool.WalletAccountingExport'
    applicationName = 'LedgerExport'
}

dependencies {
    implementation project(':ledger-export-lib')

    runtimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes 'Implementation-Title': 'Wallet Ledger Export Tool',
                'Automatic-Module-Name': moduleName,
                'Implementation-Version': archiveVersion.get(),
                'Main-Class': application.mainClass
    }
}

run {
    args = [ 'mainnet', '-o', 'omnicore-export.ledger', '-m', '../test-data/addresses_accounts.csv']
}

graalvmNative {
    binaries {
        main {
            imageName = applicationName
            //configurationFileDirectories.from(file('src/main/graal'))
            buildArgs.addAll('--allow-incomplete-classpath', '-H:EnableURLProtocols=http,https')
            runtimeArgs.addAll('mainnet', '-o', 'omnicore-export.ledger', '-m', '../test-data/addresses_accounts.csv')
            javaLauncher = javaToolchains.launcherFor {
                languageVersion = JavaLanguageVersion.of(17)
                vendor = JvmVendorSpec.matching("GraalVM Community")
            }
        }
    }
}

